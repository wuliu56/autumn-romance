label ch46_main:
    #"这是第4.6章内容"
    #Scene1
    scene bg playground2 with fade
    #play music "audio/012.mp3" fadein 1.0 fadeout 1.0
    m "真是盛况啊..."
    "操场上的音乐渐渐停止，穿着军训服的新生如潮水一般涌出来。"
    "去年我也是这人潮中的一员吗？"
    m "不知道什么时候才能找到林小莫啊。"
    m "军训总结大会的时候可不可以带手机来着？"
    "总之，还是先给他发条〇信吧。"
    "11:40A.M."
    $m_name = "阿酱"
    m "在吗？出来了吗？"
    m "我在体育场南侧的门等你。"
    "看不出小莫已读还是未读，但是软件上显示她收集4G在线。"
    $m_name = temp_name
    m "要是她看见消息就好了啊。"
    "人潮渐渐平缓，余下的学生和其他人员三三两两地离开操场。"
    m "嗯？"
    show l military normal medium:
        xalign 0.5
        easeout 1.0 xalign 1.0 alpha 0.0
    "我看到一个小巧的穿着军训服的身影从道路的另一侧溜过，带着一点偷偷摸摸的氛围。"
    "那不是林小莫吗？我赶忙追了上去。"

    scene bg gym side1 with fade
    m "喂，林小莫。等我一下啊。"
    m "这么着急去哪啊。"
    show l military angry medium with dissolve
    "林小莫开始还有点逃跑意味般加快了脚步，不过被我追上之后就放弃了。"
    l "..."
    m "身上没有带手机吗？我先送你去宿舍吧，今天的太阳还蛮大的。"
    "说着，我撑开了遮阳伞打在林小莫头上。"
    "林小莫撇撇嘴。"
    l "我看到消息了...不用这么着急来接我明明也没关系。"
    l "我还得回宿舍换衣服，洗，洗个澡什么的。"
    m "哎呀，真是抱歉。我太心急了，哈哈哈。"
    "我打着哈哈蒙混过去。我真是有点太着急了，让人家感到困扰了。"
    "明明还以为准备了阳伞就能表现自己细心了，我还是太自以为是了。"
    "不过..."
    m "我只是想早一点见到你嘛，这样就可以和你一起待更长时间。"
    show l military normal medium with dissolve
    l "嗯。"
    l "（见到你，其实我也很开心。）"
    l "（能和你多呆一会真是太好了。）"
    m "嗯？"
    l "没什么。"
    "真是的，你的声音我太敏感了。即使是自言自语我也不会漏听的。"
    "不过在这不知道怎么接话的当口，还请允许我假装没听见吧。"

    #Scene2
    scene bg summer road2 with fade
    play music "audio/007.mp3" fadein 1.0 fadeout 1.0
    show l summer normal medium with dissolve
    l "出发吧。"
    m "先去地铁站吧。要不要坐校车去？"
    "说着，我看向前方路口的停靠点。"
    m "哇，人好多啊。"
    "毕竟新生才刚刚军训完，这是他们开学后第一次有机会逛逛这个城市吧。"
    l "不要。人好多。"
    m "哈哈，也是。"
    m "看来小莫和我一样，都是不喜欢扎进人堆里的那种家伙呢。"
    l "嗯。而且，天气很热，会出汗。"
    m "女生果然会在意这些地方呢。"
    m "不过走到地铁站的话也不是一歩两步的距离呢，这太阳下估计也会出汗就是了。"
    show l summer angry medium with dissolve
    l "呜呃。"
    "林小莫表情没怎么变，却从嘴里漏出来有点嫌恶的情绪。"
    m "那，我骑车载你怎么样？虽然是自行车就是了。"
    show l summer surprised medium with dissolve
    l "真的吗？谢谢学长。"
    l "不过，这样会不会不太好？"
    l "明明这么热的天，学长还特地骑车载我。"
    m "没关系的，大男人嘛。"
    m "要么到时候你请我杯奶茶就好了。"
    m "啊，不过奶茶我不太喝的惯。一杯咖啡或者茶什么的就行了。"
    show l summer happy medium with dissolve
    l "嗯！"
    "......"
    scene bg road3 with fade
    "风拂面吹来。我艰难地踏着踏板。"
    m "呼，啊，呼——"
    "明明林小莫是个很轻的家伙，不过多坐了一个人的自行车还是比一个人时难骑多了。"
    scene bg road3 with vpunch
    l "啊呀！"
    "自行车轧过一颗石子，小小地震了一下。"
    scene bg mall8 with fade
    "呲——"
    "终于到地铁站了，我停下车。幸好，一路上没有摔下来，也没有磕到什么东西。"
    "虽然一直感觉摇摇晃晃的就是了。"
    m "林小莫，感觉怎么样？"
    "我的车技也不怎么样，可能坐起来有点难受。"
    show l summer surprised medium with dissolve
    l "坐在后座，有点疼..."
    "林小莫捂着自己的屁股。"
    "确实，那地方本来也不是给人坐的。校园里还在修路，一路上也不平坦，应该很难受吧。"
    show l summer happy medium with dissolve
    l "不过，很有意思。"
    l "原来坐在自行车后座上是这种感觉。"
    m "呃，咯着疼的感觉？"
    l "不是啦。就是骑起来晃晃悠悠的，风一阵一阵吹过来的感觉。"
    l "原来电影里那种，自行车后座坐起来其实是这种感觉。"
    m "那么下次还坐吗？"
    "我有点坏心眼地问。这种座位坐着舒服才有鬼了。"
    show l summer normal medium with dissolve
    "林小莫悄悄别开视线。"
    l "有机会的话...再说吧。"
    "哈哈。"
    m "话说你用的是地铁卡还是乘车码？"
    "说着我领着林小莫往检票机走。"
    m "有坐过这个城市的地铁吗？"
    l "普通的，乘车码。"
    show l summer surprised medium with dissolve
    l "现在还有人会用地铁卡吗？"
    m "大概没有多少了吧。"
    m "话说，有没有什么想去的地方？宁城有名的观光地还不少呢。"
    show l summer normal medium with dissolve
    l "并没有什么特别想去的地方。"
    l "而且，我们学校好像在市郊，去那些地方是不是相当麻烦..."
    m "坐地铁的话走得太远确实要花不少时间呢，而且挤得也难受。"
    m "不过要是想方便一些的话，附近就有不错的商业中心。"
    m "嘿，车刚好来了。走，这个方向一站就到了。"
    l "嗯。"

    #Scene3
    scene bg momiji12 with fade
    play music "audio/006.mp3" fadein 1.0 fadeout 1.0
    "虽然我说了要林小莫请我杯咖啡什么的，但是眼都不眨就是两杯Starduck也确实..."
    "该说不愧是有钱人家的小姐呢，消费观念都完全不同。"
    show l summer normal medium with dissolve
    "林小莫用勺子小口品尝着冰激凌盖，而我则是把纸杯贴在脸颊上给自己降温。"
    "冰美式的苦味和温度让被这两点钟的暑气热昏头的我恢复了清醒。"
    m "林小莫果然也喜欢甜食？"
    l "嗯。"
    m "女生们是不是都喜欢这种冰激凌，蛋糕什么的？"
    l "不清楚。只是，不会讨厌甜的吧。"
    l "学长居然点了美式。不会觉得苦吗？"
    m "当然苦啊。怎么可能不苦。"
    m "不过正是因为苦，咖啡才有值得品味的地方。要尝一口吗？"
    "说出装模做样的话，我不禁回想起小时候第一次喝黑咖啡，一口吐出来的情景。"
    show l summer surprised medium with dissolve
    l "呃。不能接受。"
    l "那东西不加糖根本喝不下去。还有一股烟味似的味道。"
    m "怎么，你喝过啊。"
    show l summer angry medium with dissolve
    "林小莫微微皱了下眉。"
    l "嗯。"
    l "那个，学长..."
    m "嗯？"
    show l summer shy medium with dissolve
    l "星冰乐...冰激凌要尝一点吗？"
    "林小莫脸红红的，试探般看着我。"

    menu:
        "是因为我一直盯着她看，误以为我想尝尝吗？"

        "算了":
            call choice_9a

        "试试吧":
            call choice_9b

    m "那么，休息得也差不多了，先去哪逛逛吧？"
    "说实话虽然早饭吃的迟，但到这个点其实还没吃午饭，我也有点饿了。"
    "不过总不能刚从Starduck出来又跑进另一家饮食店吧。早知道刚才自己点块三明治了。"
    "话说林小莫回宿舍的时候吃过午饭了吗？"
    show l summer surprised medium with dissolve
    l "那个，是什么？"
    "林小莫趴在商场的栏杆上，指着对面的游戏厅。"
    scene bg mall3 with wipeleft
    m "抓娃娃机吗？不知道吗？"
    show l summer surprised medium with dissolve
    l "好像听说过？"
    l "把硬币投进去，然后娃娃掉出来的机器？"
    m "呃，你说的那个与其说是抓娃娃机，倒不如说是扭蛋机..."
    l "扭蛋机？那是什么？"
    m "不过也好，我们先去游戏厅看一看吧。"

    #Scene4
    scene bg mall9 with fade
    show l summer surprised medium with dissolve
    l "这里叫XXX游戏厅，也就是说..."
    l "这边的摆着的东西，都是游戏机？"
    m "对，都是类似的东西。"
    l "我还以为这种地方小孩子会多一些。"
    "实际上周围还是和我们一样的年轻人多一些。都是情侣，或是有男生的团体为主。"
    m "这附近就是大学城嘛。"
    m "而且，今天还不是周末呢？"
    "我和刚军训完的林小莫下午都没课才跑出来玩的。"
    show l summer normal medium with dissolve
    l "[m_name]，这个！"
    "林小莫的声音里少许流露出一点激动。"
    scene bg momiji13 with fade
    "林小莫趴在娃娃机前，鼻子几乎要贴上玻璃。"
    show l summer happy medium with dissolve
    l "哇，这就是娃娃机吗？"
    "我带看孩子般的微笑点了点头。"
    m "你试试看能不能抓上来一两个吧。"
    m "硬币没了还可以到那里换。"
    "要是我一个人我是绝对不会继续换的。"
    l "..."
    "等我回过神来，林小莫已经全神贯注地投入到抓娃娃的操作当中去了。"
    "她的操作到处都显出新手的无知与笨拙。"
    "第一回连控制杆都没找准。"
    "第二回操之过急，一下子就把爪子甩过了头。"
    "第三回，畏畏缩缩的林小莫又因为太过谨慎错过了时机。"
    show l summer down medium with dissolve
    l "..."
    "林小莫一言不发。"
    "..."
    "又是几回过去，显然，林小莫对这个游戏不上手。"
    "别说抓上来了，连奖品的边都没蹭着几次。"
    l "嗯..."
    m "那个..."
    "我想了想，还是先闭上了嘴。"
    "我现在去劝的话，林小莫肯定不会听的吧。"
    "......"
    "这是第几次了呢？"
    "爪子从奖品别上擦过，还是什么都没能抓起来。"
    l "..."
    m "需不需要我来帮你一下..."
    l "..."
    "......"
    "不知道多少次后。"
    "这一次爪子的一边勾到了商品，但只是稍微让它动了动，并没有抓起什么东西。"
    "林小莫已经跑了几趟兑币机。"
    "这金额已经让我感到有些害怕了。"
    l "嗯..."
    m "差不多了吧。"
    m "钱倒是小事，你在这里已经站了很久了，不如稍微歇歇吧。"
    play music "audio/007.mp3" fadein 1.0 fadeout 1.0
    "我伸出手，轻轻放在林小莫肩膀上。"
    show l summer down medium with hpunch
    "林小莫轻轻甩了甩肩，我却没把手移开。"
    "林小莫不为所动，把游戏币投进去，开始下一轮的操作。"
    m "不要在这上面浪费时间了吧。"
    "还有不要浪费钱了啊！"
    l "...不要。"
    m "那么让我来帮你抓吧。我保证抓到你想要的。"
    l "不要，我要自己抓。"
    m "..."
    "失败，又一次。"
    "我有点钦佩林小莫的韧性。但我真的看不下去了。"
    "我靠到她身边，把手搭在林小莫握着操纵杆的手上。"
    "林小莫回头看了我一眼，又一言不发地转过头去。"
    "林小莫的身体小小的，就像人偶一样。"
    "她的手掌也小得仿佛一只手就能全部包住一样。"
    "我轻轻引导着她的手，凭着记忆中（从动漫里学来的）几次抓娃娃的经验，想方设法地找到个正确的位置。"
    show l summer surprised medium with dissolve
    l "..."
    "我甚至能感受到林小莫的呼吸变得稍快，稍重了一些。"
    "或许我的鼻息吹到了她的脖子上吧，林小莫感觉痒似的缩了缩脑袋。"
    "吱吱——"
    "机械爪移动着..."
    "唰——"
    m "呼..."
    "运气真好。终于抓到了。"
    "或许运气也不好，这已经是多少次了？"
    "总之，感谢恋爱喜剧里经常出现的娃娃机情节，用机械爪勾住包装边缘的的技巧还是挺有用的嘛。"
    show l summer happy medium with dissolve
    "林小莫脸上也浮现出明显的惊喜之情。"
    l "[m_name]学长，看！我抓到了。"
    m "太好了，恭喜你。"
    "我带着哭笑不得的表情回应她。"
    show l summer normal medium with dissolve
    l "啊，让学长白白陪我在这里耗了那么长时间，我却在这里高兴...是不是不太好。"
    m "没关系的。我今天本来就是陪你一起出来玩的。"
    "话说你在担心我之前心疼一下自己的钱包吧！"
    "就算我在心里吐槽，这个土豪女也不会听得见就是了。"
    l "不过还是要谢谢学长，没有学长，我可能真的到关门都抓不上来呢。"
    "你还要抓到关门啊！我背后冒上来一阵冷汗。"
    l "下次，我一定要自己抓一个上来。"
    show l summer happy medium with dissolve
    l "这个兔子玩偶真可爱...上面连着挂链，是挂饰呢。"
    m "很可爱啊。挂在包上怎么样？"
    "女孩子应该都喜欢在自己的包上挂点小饰品什么的吧？"
    l "这个，太可爱了..."
    l "挂上去的话有点害羞呢。"
    m "没关系啦，林小莫你也很可爱，小兔子和你风格很搭呢。"
    show l summer shy medium with dissolve
    l "啊，呜嗯。"
    l "谢谢学长。"
    show l summer shy medium:
        easeout 1.0 xalign 1.0 alpha 0.0
    l "我，我去摘朵花。"
    "林小莫红着脸逃走了。"
    "我刚才好像说了有点大胆的话啊..."

    #Scene5
    scene bg restraurant3 with fade
    play music "audio/006.mp3" fadein 1.0 fadeout 1.0
    "......"
    "我端着托盘，坐到了某KFD的一张桌前。"
    "几分钟前——"
    m "玩了一圈也有些累了，要不要去哪坐会儿？"
    m "林小莫你呢？想吃点什么吗？"
    show l summer normal medium with dissolve
    "林小莫点点头。"
    m "那有什么想去的地方吗？"
    l "我不怎么熟悉这里。"
    l "去学长想去的地方就好了。"
    m "中学的时候没有和同学们出来逛街什么的吗？"
    "林小莫摇了摇头。"
    m "这样啊..."
    l "都交给学长就好了。"
    "我也没怎么去过自宅以外的地方啊...又不是现充..."
    "还是保守一点的选择好了。"
    "于是——"
    "我们来到KFD，点了一份当季限定的双人套餐。"
    "林小莫似乎没来过这种快餐店，一副有点兴趣盎然的感觉。"
    l "学长常常来KFD吗？这里有些吵闹啊。"
    m "多多少少来过吧，大部分人。"
    m "倒不如说像林小莫你这样没怎么来过的反而是少数。"
    m "怎么样，还合你的口味吗？虽然只是廉价的食物。"
    l "这种味道有些新鲜，不过，好吃。"
    show l summer surprised medium with dissolve
    "虽然嘴上这么说着，林小莫的表情却有点微妙。"
    m "怎么了吗？"
    show l summer angry medium with dissolve
    l "......"
    m "那，下次还要来吗？"
    l "这个...算了。"
    l "这里的食物太油腻了，用手抓着吃感觉还会粘上味道..."
    m "那，林小莫出来吃饭会去什么样的餐馆呢？像是格调很高雅的西餐厅之类的？"
    l "不全是吧。"
    l "不过，大概是是安静些的地方。"
    m "这样啊..."
    "要是和社团里的人一起出去玩的话会是什么感觉呢？林小莫能不能顺利融入进来呢？我突然想到这种还远得很的问题。"
    "林小莫和我们这种二次元也不是一类人，真的能把她拉进动漫社吗？"
    "哦，对。我这么想跟林小莫亲近，还来拉她出来约会的理由不就是想让她入社吗？"
    "..."
    "..."
    "糟糕，一不小心就想的有点远了。"
    m "那个..."
    "一陷入沉默，对话就感觉难以进行下去。得想些什么话题。"
    m "要不要加入我们星辉动漫社？"
    show l summer surprised medium with dissolve
    l "嗯？"
    "搞砸了啊...不愧是我，聊天鬼才。"
    m "啊，不是，就是想问问看..."
    m "对了，你有在看我之前给你推荐的的那部"
    show l summer normal medium with dissolve
    l "最近不是军训汇演要彩排很忙嘛..."
    m "啊，也是啊，我太着急了。"
    l "...所以到现在只看了一季。"
    l "几乎花了我一晚上呢，结果第二天一直没有精神。"
    m "哈哈，明明不用勉强自己也没关系的。"
    l "不是勉强..."
    l "说实话，很有意思。"
    m "真的吗？"
    l "嗯。"
    "以前，我偶尔会热血沸腾地把自己喜欢的动漫和游戏安利给周围的人。"
    "但是从来不会有人认真地去看，哪怕只看完一集。"
    l "那个学园背景的设定很新颖，故事展开的方式也和我看过的电影，小说之类不同..."
    l "我一直以为二次元就是动漫，是有点幼稚的东西。"
    m "谢谢你。"
    l "嗯？"
    m "说实话，喜欢动漫，喜欢二次元确实有点幼稚。"
    m "明明那些东西在现实里都不可能存在。"
    m "但正是因为与现实不一样，才会那么美好，那么让人沉醉其中。"
    show l summer surprised medium with dissolve
    l "比起现实，学长更向往故事里的世界吗？"
    m "那当然了。"
    l "那就是，讨厌现实的意思吗？"
    m "不...不太对吧。只是对现状有点不满，有点不切实际的幻想罢了。"
    "但其实，我很讨厌现实。讨厌一无所成的自己。"
    m "说到底，二次元不过是一种表现形式罢了。"
    show l summer normal medium with dissolve
    l "动漫社里，喜欢二次元的人，都和学长一样吗？"
    "我脑海中闪过他们的脸。他们沉浸在虚构的世界中时，是什么样的表情呢？"
    m "大概，有相似之处吧。"
    m "既然都是同好。但也仅仅如此。"
    l "关于加入动漫社...是叫星辉动漫研究社吧？"
    m "嗯。"
    l "我想加入，可以吧？"
    m "没关系，我也知道你不情愿..."
    m "嗯？什么？你要入社？"
    "我一时间甚至没反应过来。"
    show l summer happy medium with dissolve
    l "嗯。"
    m "为什么改主意了。，。"
    m "啊，我就是问问看。"
    l "没什么，特别的。只是因为学长推荐给我的作品都很有意思。"
    l "（比起现实，我也有点想去那一边的世界看看呢。）"

    menu:
        m "嗯？"

        "装作没听见":
            call choice_11a
            jump ch_l #进入主线

        "接话":
            jump ch_l1 #进入支线一

    return

label choice_9a:
    "不过算了吧，也不是亲密到可以间接接吻的程度。"
    m "不用啦，不是特别想。"
    l "哦。"
    m "就是那个，我不太擅长吃甜的的。"
    "感觉直接这么拒绝也不太好，还是找点借口吧。"
    l "所以才特意点了美式呢。"
    m "哈哈，就是这样。"
    return

label choice_9b:
    m "那好啊。"
    m "不过啊，这样可算是间接接吻啊。"
    m "不介意吗？"
    "林小莫听了我的话，手微微一缩，脸上更红了。"
    l "没，没关系的。"
    "说着，舀了一勺冰激凌，林小莫朝我递了过来。还要喂我的嘛！？"
    "这样对待他是不是不太好，明明只是前辈后辈的关系？"

    menu:
        "不过直接拒绝可能更不好吧..."

        "就这样接受吧":
            call choice_10a

        "找个借口拒绝":
            call choice_10b
    return

label choice_10a:
    m "那，恭敬不如从命了。"
    m "啊呜——"
    "真是舀了好大一勺啊，感觉寒气直逼脑门，微微有点刺痛。"
    "不过我的脸颊也在发烫，这样降降温可能正好。"
    "林小莫低下了头，看不见她的表情。"
    "不过，从发间露出的耳朵已经红透了啊。握着勺子的手也在微微颤抖。"
    "明明会害羞就不要这样做啊。"
    "啊啊啊，真是的。"
    m "味道挺不错呢。"
    m "这种天气吃冷饮说不定刚刚好。"
    show l summer normal medium with dissolve
    l "嗯。"
    return

label choice_10b:
    m "呃，还是算了吧。刚才开个玩笑。"
    m "其实我不太吃得了甜食。"
    m "谢谢你的好意。"
    show l summer normal medium with dissolve
    l "这样啊..."
    "林小莫收回手，露出有点遗憾的表情。"
    m "就是这样。"
    m "不过林小莫居然做会这么大胆的举动，我还真没想到呢。"
    show l summer shy medium with dissolve
    "林小莫别开视线，脸又红起来。"
    l "也没什么..."
    return

label choice_11a:
    "我装作没听清后一句话。"
    "不想让别人听到的话，就不要嘀咕出声啊。"
    m "..."
    "即便你这么优秀的人，也会觉得三次元的世界有些讨厌吗？"
    "......"
    return
