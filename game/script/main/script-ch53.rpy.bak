label ch53_main:
    #"这是共通线第5.3章内容"
    #9.13日
    #场景：学校，街道，酒吧，宿舍
    #人物：友人A,B，夏何，男主，舍友
    #Scene 1
    #场景：宿舍
    #场景：街道
    #人物：夏何，友人A,B
    "夏何视角"
    scene bg road3 with pixellate
    play music "audio/004.mp3" fadein 1.0 fadeout 1.0
    "他也在酒吧打工了几天了，虽然出了不少岔子，但是学的还是挺快的，难道说他有成为现充的天赋？不过那个强打精神装出笑脸的样子还是挺糗的，不如说一眼就能看见的不乐意。虽然很对不住他，感觉像是我在无理取闹，但确实没有敷衍我，今天下班了再请他喝一杯吧。"
    "友人A" "「小何，何儿，夏何，夏何！」"
    show x summer normal medium with dissolve
    x "嗯？我在听。"
    "友人B" "「你怎么可能在听啊，双眼无神，叫了你那么多声都没反应，明显又在想什么事情，怎么了？有什么事情是不能告诉我们的？家里？学习？编舞还是……」"
    x "没啥事情，就少八卦了，我在想期中考试试题可以吗？刚才说了什么呢？"

    "又走神了，自从出了这个破事，不得不每天脑子里想着怎么样能让他蒙混过关都差点出现我心里有他的错觉了。虽然在普遍单纯的意义上来说是没有问题的。"
    "友人A" "「我们在商量社团第一次全社见面会是什么时候，大一的新人不是也放假了吗？不确定一下这地方一起出来玩一下吗？」"
    x "我可能没空，要去打工，见面会就缺席吧。到时候集体排练的时候就见到我了。"
    "虽然很想去，但是要是真被搭讪上了，就会变得更加难办了，不如先不去，等以后慢慢认识，也减少了，因为慌张而暴露本性的可能。"
    "友人B" "「街舞社灵魂人物不愿意参加新人见面会可不行。不少人可是因为你在这个社团才进来的哦，你不来不是打击人家自信心吗？」"
    "友人A" "「对呀，再说了，大一有不少长得挺帅的学弟，你不是说你没有男朋友吗？」"
    "万一有个你看上眼的跟你表白了那不就好了？就算没有那也可以扩展圈子嘛，你不是最喜欢这种事情了吗？"
    "友人B" "「难道害怕之前那个孩子啊在意吗？」"
    "我是因为他吗？不可能吧。我刚才想的原因里根本没有他的成分……虽然刚才给出的理由也完全站不住脚。我不知道为什么，就是不想去，与其说是他，还不如相信是我的社恐基因复苏了。"
    x "不是啊！！就是单纯的不想去。对了，那天我还要去打工，没时间去，就说我忙就行了。"
    "友人B" "「还是之前那个酒吧？」"
    x "嗯，对，就是去酒吧打工，你们就自己决定，自己好好玩吧，不用担心。年末的时候我肯定会去的。我先回去了，你们这俩个八卦狂魔。（离去）"
    "友人A" "「啊这，主角不去可不行啊怎么办？」"
    "友人B" "「夏何不是在酒吧打工吗？我们到时候直接去他们酒吧就好了吧？先不告诉她就行了，到时候谁管你是招侍，直接拉过来一起玩就好了。」"
    "友人A" "「好主意，等下，去我和店长联系确认一下。」"
    "夏何视角结束"

    #Scene 2
    scene bg computer with fade
    play music "audio/008.mp3" fadein 1.0 fadeout 1.0
    "不知不觉，已经在酒吧工作有些时日了，生活也慢慢习惯了夏何和她所带来的现充世界的入侵。"
    "被迫脱离原本熟悉的圈子，被暴露在了让人不自在到窒息的陌生环境里，我居然能坚持到现在陪着她胡闹。但无法否认的是我在酒吧确实变得更加习惯于和人打交道了，不再那么容易呆呆地立在原地，不再那么容易沉浸到自己的世界以至于忽视了别人说了什么。"
    "酒吧的打工虽然挤占了我的大部分课余时间，但是夏何却那么来劲，我虽然不是日系轻小说男主，可是我拒绝别人的请求还是很难办到，更何况本身也不是什么大事情。"
    "但是现在我和夏何的关系肉眼可见得变得暧昧起来了。"

    "虽然之前为了维持我的现充人设在夏何朋友面前已经暧昧到被认为是情侣都不会奇怪的地步了。从来没有过这种担忧的我突然感觉到可能我不得不去确定或者澄清我们的关系，要不然，这种生活还不知到要持续到什么时候。也不能确定我不会露出马脚。"
    "今天去酒吧就和夏何商量一下吧。"
    m "我去打工了，晚点回来，有什么需要带的吗？"
    "舍友" "「嗯？？！好小子居然这么懂事了？！和可爱的学姐打工就这么开心吗，人」"




    m "哪有？就随便问了一句。这么惊讶干嘛，咱俩是没说过话吗？"
    "这是我第一次和舍友完成了正常友好的交流。完全就是普通的宿舍日常，确实我努力了很久得到的。"
    "舍友" "「哪里不是？脸上都挂着傻笑了，笑得比你看动漫还开心。」"
    "我在笑吗？我开心什么呢？"
    m "我乐意，有没有，没有我就走了！"
    "舍友" "「呦呦，这么着急？难得你主动搭话，帮我从你酒吧带点吃的吧。回头我带你出去玩。女人能改变男人是真的啊，都能把你改造了，实在厉害。」"
    m "别笑我了，我出去了，想起来啥直接给我 打电话就行了。"
    "第一次在非必要的情况下主动搭话。感觉……还不错？"
    "舍友" "「这个学姐到底是何方神圣？能把他变成这样。」"

    #Scene 3
    #地点：酒吧
    #人物：友人A,B，夏何，男主，男路人A，B
    scene bg bar1 with fade
    play music "audio/004.mp3" fadein 1.0 fadeout 1.0
    "酒吧温馨柔和的灯光照亮了吧台，门外的夜色渐浓。明明是酒吧，却给人萌生出安心的归属感，热闹但是不吵闹，原本以为该是一群人喧嚣，耍酒疯的地方，在店长的经营下却感受到了真实的愉快，日子仿佛在这里过去就不吃亏。"
    "今天不知怎得客人少得奇怪。平时受宠的被顾客们争相演奏地钢琴落寞地躺在一角，只有在店长陶醉地演奏着，正经的前辈在仔细地擦着一个高脚杯，仿佛电视剧里常见的那样——虽然那只高脚杯已经被擦过无数遍，甚至要怀疑会不会有被擦碎的风险。夏何坐在角落趁着无人自己偷偷看着同人漫画，脸上露出了收不住的奇怪表情，要是用漫画的表达方式的话，可能就是表情融化了吧？而我盯着眼前的一杯记不住名字的酒发呆，脑子里想的全都是这几天发生的事情。"
    "收拾了一下思路，眼前的情况——已经完全超出日常的范畴了，原本只是想找几个志同道合的同好一起能够聊天，能够性格变得开朗点。但是却摊上这么多事情"
    "真是一言难尽，现在可能是难得的清闲时光吧。"
    show x summer normal medium with dissolve
    x "店长，今天怎么没什么人啊？"
    "夏何终于从漫画里脱离出来，收起了一眼就能看出来不对劲的表情， 抬起头对着沉浸在音乐里的店长大叔说到。"
    "确实，平时现在是最忙的时候，下班的上班族，昼伏夜出的无业游民，上学休息的大学学生，往日里熟悉的身影和热闹在此刻却几乎空无一人。"
    "店长" "「嗯，差不多到时间了。」"
    "店长抬眼看了一表，站起身来，悦耳安心的音乐也戛然而止。"
    "店长" "「各位，今天要打烊了，晚场有人包场了，所以请大家早点回去休息吧。感谢光临。」"
    show x summer surprised medium with dissolve
    x "啊啊啊？店长平时不是不会包场吗？为什么今天突然就这样，再说了为什么不提前告诉我们呀，好歹准备一下。"
    "店长" "「因为这是客户要求啊，要不是因为小何我也不会接包场的。」"
    "？？？因为夏何而夏何不知道？"
    x "难道是……迎新派对？？"
    "夏何的脸色变得煞白，嘴角不自然地抽搐起来，慵懒的眼神里除了惊讶还多了一丝恐慌。"
    "店长" "「对呀，她们说你要打工，我说可以直接来这里办派对，就算沾沾年轻人的活力了，不告诉你是为了给你个惊喜。」"
    show x summer happy medium with dissolve
    x "这……样啊。真的是没想到，谢谢店长对我这么用心。"
    "夏何脸上挤出了一个笑容，看起来很真，却掩不住向后缩的脖子和瞟向我的眼睛。"
    m "店长，我今天还有点事情就先回去了。"
    "要是遇到夏何的朋友们就麻烦了，与其坐以待毙，等着穿帮或者上演琼瑶大戏，还不如早点回家推游戏。今天还有新的游戏更新。再说了，真要是到了派对的氛围里，我绝对会格格不入的。"
    show x summer normal medium with dissolve
    x "那你忙吧，我来帮你干活。"
    "店长" "「今天店里比较忙，小伙子要是有空我还是希望你能留下来。还是说怕见到小何的朋友们？」"
    m "那……那肯定不是啊。"
    "店长" "「那事情真的不能……」"
    "还没等店长说完话，就把外面就突然出现了一群人打断了店长的话。"
    play music "audio/009.mp3" fadein 1.0 fadeout 1.0
    "推开门，夏何的两个死党带头走了进来。"
    "友人A" "「老板，我们来啦！之前预定的包场真的是麻烦您了，您这里平时也不提供包场，这次能够用来当作我们的迎新场地，真的是谢谢您了，等会还要麻烦您了。」"
    "友人B" "「何宝！！Surprise，特意没告诉你，就想给你个惊喜，这次我宣布，所有人自助酒水，服务员们也一起来玩吧。」"
    show x summer surprised medium with dissolve
    "夏何两腿僵在了原地，她朋友拉着她往前走，却没有移动，两眼刻意地避开了人群，宅的属性在这个时候暴露了。眼睛里看到的不是惊恐而是呆滞，拳头不经意地攥紧，她没有在害怕什么，而是被眼前突如其来的状况冲击到忘记了伪装，遵循了自己宅的本能。"
    "友人B" "「怎么了？何儿？太吃惊了？」"
    m "咳咳，夏何你去吧，我来忙，不用担心了。"
    "我沉着嗓子，努力捏出了一个不像我的低沉嗓音，转过身去向夏何说到。现在的我和第一次见到她们的我已经截然不同了。我有自信她们认不出来我。"
    "友人A" "「这不是之前那个大二小哥哥吗？你也在这里打工？」"
    "完了，被认出来了。"
    m "你，你……你认错人了。我已经大四要毕业了。"
    "友人B" "「真的吗？」"
    m "当然是啊！"
    "完了忘记伪装声音了。"
    "友人A" "「不就是那天见到的小哥哥吗？来一起吧！」"
    "我用绝望的眼神望向夏何，夏何却用空洞的眼神回应了我。"
    "完了，这个人的脑子宕机了。"
    "振作点夏何！"
    show x summer surprised close with dissolve
    "我快步走向夏何面前，从她的朋友手里拦下了她的手"
    m "我和夏何先去收拾一下其他地方，你们顺便看看还有什么需要的找吧台就行了。我们把活先干完，要不然要挨店长骂了，学姐等会见！"
    "拉着夏何，跑到了后厨，夏何眼里还满是脆弱。"
    m "夏何，冷静点，醒醒。"
    "夏何从魔怔的状态里出来，眼睛里的恐慌显露无疑。"
    m "至于这么慌张吗？我还在这里，最差能怎么差啊。"
    x "我……我…不知道。"
    m "你不是人见人爱的现充夏何吗？你现在比改造前的我还要自闭。"
    show x summer normal close with dissolve
    x "你以为我害怕什么啊！！还不是怕你没有搞好露馅！"
    "夏何少见地不理智地抱怨了起来。她想用抱怨来掩盖自己的慌乱"
    m "咱们总要出去的，一起去面对这些事情吧。这次是一场遭遇战，你训练了我这么久不就是为了这个时候吗？"
    show x summer down close with dissolve
    x "可是……你还没准备好。"
    m "你就这么不相信我吗？"
    hide x with dissolve
    "第一次，我在和夏何的相处里占据了主动权。"
    "我拉起夏何的手向外面走去。"
    "当我们打开门走出来的一霎那，全场的焦点却突然聚集在我们身上。安静，就像是吵闹的猴群突然安静来躲避天敌，这是人类来自血脉的本能，是感受到了变化的自保，可是这次，他们可能才是猎人。虽然他们可能甚至没有猎人的自知——这本就是一场自我臆想出来的战斗。"
    "友人A" "「总算出来了，你们在里面呆了可好久啊。」"
    "友人B" "「怎么了小两口聊天了没完了是吗？」"
    show x summer normal medium with dissolve
    x "我不想再说一遍了！"
    "友人AB" "「你们不是情侣！！！」"
    x "知道就好。"
    hide x with dissolve
    "两人却把我拉到一旁开始审讯。"
    "友人AB" "「那小哥哥是在追夏何吗？」"
    m "嗯……保密。"

    menu:
        "友人AB" "「这么说就是了？」"

        "承认":
            m "嘘嘘，我还没有开始追夏何。不能算！（小声）"
            "友人AB" "「那你可要加油了竞争者可不少哦。」"
        "否认":
            m "怎么可能啊，我和夏何学姐真的只是朋友关系！"
            "友人AB" "「真的吗？不会是不好意思吧？不管怎么说要认真点。」"
        "含糊其辞":
            m "我不知道，我不能说，这种事情我也不能乱说只能说懂得都懂。"
            "友人AB" "「打太极拳说这种话就没意思了，不过不想说也不强迫你啦，但是小心后悔哦。」"

    show x summer normal medium with dissolve
    x "好了好了，别傻愣着了赶紧开始吧。"
    "夏何和我被拉入人群。"
    hide x with dissolve
    "一通听不懂的演讲，一通没有意义的自我介绍，派对就开始了。"
    "应该说不愧是街舞社吗？大家都一看就是有舞蹈基础，而我，实在是没有跳过一次舞。"
    "蹦迪也只会随便乱跳。"
    "但是好像酒吧里的人也是这样？"
    "看着平日里安静的酒吧变成了影视剧中常见的样子。有种恍如隔世的落寞淹没了房间。"
    "但是这种原本与我无关的热闹却在此时波及到了我，我也成为了热闹的创造者，热闹的参与者。明明一直是个局外人，却在这几天里突然融进了一个完全没有想象过的世界，不是到是好是坏。"
    show x summer normal close with dissolve
    "夏何突然挪到了我的身边，我成了她的舞伴，虽然我不知道蹦迪里舞伴这种东西是不是需要的。"
    if x_appeal == 2:
        menu:
            x "他们给你说啥了？"

            "「什么也没有」":
                call choice_15a

            "如实告知":
                call choice_15b
        #转入夏何线
        if x_flag:
                jump ch_x
    else:
        menu:
            x "他们给你说啥了？"

            "「什么也没有」":
                call choice_15a

    scene bg computer with fade
    "学习逐渐忙碌起来，社团的事情也越来越多，生活的节奏变得越来越乱了。"
    "手机铃声响起"
    "夏何"
    m "怎么了，夏何。"
    x "今天轮到咱们俩人当班了，别迟到了，你已经忘了好几次了，我来提醒你一下。"
    m "啊这，今天我要开会抽不出来身，要不然，你帮我请个假？"
    x "开会开会开会！你都请了多少次的假了，店长骂你，把工资扣完我可不管哦。"
    m "知道了知道了，我会和他沟通好的。挂了昂。"

    "话是这么说，可是我实在是没脸见店长了，明明在打工的时候特别照顾我，现在却一次又一次鸽了他。但是漫展临近了，轮到本校办机会在校期间可没有下次了，全社团的人都在努力，我也没有理由离开啊！！"

    "算了，只能硬着头皮给店长解释了。"
    "我掏出手机，找到了店长的号码，这已经是这个周第三次打这个号码了，这月我原本只排了4天班现在已经天请假了。颤抖的手指在我想好之前就拨通了店长的号码。"
    "店长" "「小伙子，怎么了？」"
    m "店长，我今天有点事情没办法去酒吧帮忙了，想请个假。"
    "店长" "「最近几天一直请假是不是发生了什么事情啊。有什么事情可以告诉我我能帮到的一定会帮你的。」"
    m "其实，也没什么大事情，我最近在忙社团的事情，实在是脱不开身。所以不得不请假，不是对酒吧的工作有什么意见。"
    "店长" "「不错嘛年轻人，忙说明你被需要了，去干最需要你的事情就行了。酒吧的工作要是拖累你了你可以先休息一段时间，我重新排班就是了，等你想来的时候随时欢迎你，现在先把最优先的做完就好了。」"
    m "谢谢您，店长，那我到十一之前可能就不会再去了，之后我要是去的话肯定会给您说的。"
    "店长" "「好的，做自己该做的事情去吧，我就不占用你的时间了。」"
    "电话被挂断了。"
    "已经没有退路了，虽然已经暂时结束了酒吧的工作，但是在酒吧里学到的东西我还没忘。交涉交流的技巧一定会排上用场的。"
    #Scene 酒吧
    "夏何视角"
    scene bg bar1 with pixellate
    show x summer happy medium with dissolve
    x "店长我来了！他来了吗？"
    "店长" "「小伙子暂时辞职了，现在正忙着呢，兼职本来就是锻炼自己的，把本职丢了来做兼职那就是本末倒置了，再说了你希望他做的他已经做的差不多了，既然人家还是选择了更重要的事情，你就别惦记了。」"
    x "我就问问你他来了没，你怎么突然说了这么一大堆啊。辞职了那就算了，继续工作。"
    "店长" "「你没关系吗？」"
    x "我有什么关系，他做他该做的事情就行了。"
    "店长" "「我要是有这么好的老婆就好喽。」"
    x "不用担心我了，不如担心一下其他事情。"
    "夏何视角结束"
    scene bg classroom1 with pixellate
    "已经有好长时间没有见过夏何了，因为差了一个年级，而且夏何还住在外面，我又不去酒吧打工了，我们的生活现在几乎是没有交集的了，原本偶尔还会再网上聊几句天，现在也渐渐没了动静。明明才是几天的事情，却感觉已经和夏何离得很远了，感觉已经和她的关联微乎其微了。每天忙于各种事情，可忙完之后看到0条消息，和标注4天前的聊天记录，我的这种感觉就尤为明显。"
    "没空伤感了，还是继续干活吧。"
    "第二节课下课，人们在教室之间奔波。擦身之间我看到了一个熟悉的身影。她被人群簇拥着嬉笑打闹，无暇向我这里投向一个眼神，我听到的只有“那个男生和你分手也正常。”“有点可惜了”“这么快吗？该不会何姐被骗了吧”的言论。"
    "我带上了卫衣的帽子，继续向着活动室走去。"
    "我和夏何的伪装故事终于结束了，心里除了解脱的如释重负，也有不知为何安闲的空虚。和终于要和还来不习惯的伪现充生活说再见了，之后我和这个夏何不再会有关系，我只认识一个叫夏何的死宅女生罢了。"

    scene bg teaching building1 with pixellate
    "夏何视角"
    "已经有好长时间没有见过他了，因为差了一个年级，而且我还住在外面，他又不去酒吧打工了，我们的生活现在几乎是没有交集的了，原本偶尔还会再网上聊几句天，现在也渐渐没了动静。明明才是几天的事情，却感觉已经和他离得很远了，感觉已经和他的关联微乎其微了。每天忙于交际，打工，舞团的排练，可忙完之后看到0条消息，和标注4天前的聊天记录，我的这种感觉就尤为明显。可是故事总会结尾，他陪我闹了这么久，也该累了，我该给他一个结果，一个交代。"

    "于是我决定，和他“分手”，虽然本来就是装的，就没有什么伤害到谁的区别但我还是想让大家知道，让他知道，他能从这里解脱出来了。从一个不擅长的地方解脱出来干真正该干的事情。"
    "于是，在这天上午第二节课，明明没课的我却从陪着朋友们从教学楼里走过。"
    show x summer normal medium with dissolve
    "友人A" "「少见啊，夏何大小姐有空陪我们了，不用和你的小男朋友一起吗？」"
    x "少损我了，我都和他分手了。"
    "友人A" "「啊这，对不起，我说错话了。」"
    x "小问题，我甩的他。"
    "友人B" "「为什么啊，我看那个男生对你还挺好的，人也不错。」"
    x "性格不合。"
    "友人B" "「出现了万能的性格不合。」"
    "此时一个熟悉的身影擦肩而过，虽然没有看见脸，但是眼熟的衣服和改不过来的低头走路还是让我认出了他。"
    "友人A" "「那个男生和你分手也正常，毕竟你们俩都是比较有主见的。」"
    "友人B" "「有点可惜了，我看你俩挺好的，我刚刚接受了小何有了男朋友这个事实，分手这件事情，我还要适应一段时间。」"
    "友人A" "「你适应什么啊！不过这么快就分手，我怀疑何姐是被他之前的小奶狗伪装给骗了，其实是个有主见的人，控制欲强的何姐才不高兴吧？」"
    x "别瞎猜了，真不是你们想的那么黑暗！"
    "他带上了卫衣的帽子，是伤到他了吗？可是这是最快的方法，放心，之后我会和你解释的，但是现在我们只是刚分手的情侣，等到社团，另一个我会对你说明请况的。"
    jump ch_s
    return

label choice_15a:
    if x_appeal == 2:
        menu:
            x "我也不是小孩子了，你如果不愿意说的话我也不会强求……果然我还是很在意，到底说了什么？"

            "真的什么也没有。":
                x "姑且相信你一次。"

            "如实告知":
                call choice_15b
    else:
        menu:
            x "我也不是小孩子了，你如果不愿意说的话我也不会强求……果然我还是很在意，到底说了什么？"

            "真的什么也没有。":
                x "姑且相信你一次。"
    return

label choice_15b:
    m "他们问了我是不是你的男朋友，是不是喜欢你，有没有想追你之类的八卦问题。"
    x "果然，我就知道他们肯定会八卦……你，是怎么回答的？"
    "夏何突然扭捏了起来断断续续地问道。"
    m "回答什么啊？"
    "虽然知道答案，但是还是想要确认一遍她的表情出现的原因和我想的原因是否一致。"
    show x summer shy medium with dissolve
    x "就……就是问你的那些问题啊。"
    "夏何少见的脸红了。"
    m "就是按照你想的那样啊。说咱们俩是普通朋友关系。关系近了点而已。"
    "不知为何的我毫不含糊地说出了谎言。没有脸红，没有心虚，可能是我习惯了说谎，她居然信了。"
    x "好吧……不是，那就好，挺好的。"
    m "你怎么生气了？"
    x "你觉得我为什么生气了？你为什么觉得我会生气，不如说我没有生气哦。"

    "完全就是生气的样子，可是我也没有任何能够反驳的地方，不如说夏何为什么生气我完全理解不了，是嫌弃我回答的不够详细，还是我回答的内容不满意。"
    "到底是哪里出了问题？"
    m "那我该干点什么好呢？"
    show x summer normal medium with dissolve
    x "随便玩你的吧，习惯一下氛围，虽然等级跳跃有点高但是，你还是要加油哦 Kira⭐"
    hide x with dissolve
    "夏何说完便隐身在了人群里。"
    "话是这么说，对于一个假现充真死宅，这种氛围我是怎么也不可能能融入进去的吧。"
    "男路人A" "「老哥，你和夏何姐关系不错嘛。有想法吗？」"
    "已经是不知道多少次遇到这个被问到这个问题了，但还是要耐下性子来回答"
    "扯一个微笑，真诚不回避的眼神，加上自信的回答"
    m "没……没有"
    "好，终于算是完成了回答了，总的来说干的还是不错的。"
    "男路人A" "「好，你现在开始，你就是我的战友了。」"
    m "啊？？"
    "男路人A" "「其实我想追夏何，可是夏何毕竟离我太远了，而且她对大家都很好，很少会和谁单独出来，你不一样，你和夏何关系好。帮兄弟个忙，当个僚机呗。」"
    "好自来熟，完全应付不了。而且不知道为什么不想帮他。"

    scene black with pixellate
    "友人A" "「如果喜欢夏何要早点下手哦。」"
    "友人B" "「对呀对呀喜欢夏何的人可多了，竞争对手可不少。」"


    scene bg bar1 with pixellate
    "是这个意思吗？"
    m "好，好吧，我尽力。"
    "说谎了，我根本不想帮他，但是我不会拒绝人，尤其是这种自来熟的人。"
    "我甚至连他的名字都没记住，就交换了联系方式，称兄道弟，自来熟的人真可怕啊。"
    "但是，看不出来他是坏人，不如说就算是站在男性的角度来看无论是表现的性格还是外貌都是一个很好的男生。不如说他真要是追上了夏何我就解脱了，可是为什么我还是不想帮他？我有点难以理解我自己现在的想法了。"
    "男路人A" "「能帮我真的是帮了大忙了。谢谢了，等事完了一定请你出去玩。」"
    "这么着就算交了朋友了吗？有点随便过头了吧。"
    "虽然眼前的景象很欢乐但是还是感觉到了割裂感，一种我和他们格格不入的割裂感。"
    "派对迎来了高潮。气氛到达了顶点，夏何也理所应当地站在了全场中心的地方，成为了全场的焦点。虽然有过短暂的惊慌失措，但是现在她已经完全进入状态了。"
    "友人A" "「现在，一项重大的事情要发表。」"
    "友人B" "「这件事情可是很重要哦。」"
    "友人A,B" "「夏何同学过来一下。」"
    "夏何一脸疑惑地走上前去。"
    "友人A,B" "「男主角出场吧！」"
    "等等男主角？？？？一种不好的预感涌上心头。"
    "一个男生从人群里走了出来，很老套地捧着鲜花，很老套地人群从中间劈开，很老套地夏何待在原地不动。"
    "老套到我都知道下一刻要发生什么。"
    "这个男生应该是他们社团的风云人物，接下来会有长篇大论的表白，然后夏何开心地接受，我就能开心地解脱。就是刚才拜托我的老哥可能会不开心地抑郁一段时间。"
    "但是，夏何的表情很奇怪，没有一如既往的演技，没有真诚的幸福，有的是连我都能看出来的惊恐和迷茫。"
    "但是其他人没有看出来，大家被气氛裹挟着，没有察觉到夏何的不对劲，他们把迷茫看做了震惊，把惊恐看成了惊喜。夏何四顾对上的却是期待的眼神。"
    show x summer surprised medium with dissolve
    menu:
        "求助的眼神和我对上了"

        "帮助解围。":
            $x_flag = True #进主线
            call choice_16a

        "算了，对大家都是解脱。":
            call choice_16b #进支线

    return

label choice_16a:
    "真是的，我最不会拒绝别人的请求了。虽然夏何什么也没说，但是我不做点什么却感觉有些奇怪。"
    x "这是什么情况？"
    "不是为了营造氛围的flag，而是真的无法接受现状的逃避。"
    "友人A" "「你不要再装傻了，他喜欢你也是很久了哦，你不可能不知道吧。」"
    "友人B" "「你不是没有男朋友吗？表白这种事情喜欢就接受，不喜欢就拒绝嘛。」"
    "也是，我在担心什么呢，她可是夏何，在现充的圈子里混了这么长时间的人怎么可能不会拒绝表白，我在瞎担心个什么事情。"
    "可是，夏何却慌了，我没有听见那个男生说了什么，可是我却看见夏何慌了，看见夏何不知所措，看见了夏何暴露了不善与人交往的一面。"
    "夏何再次把目光转向了我。"
    m "等一下！"
    "在我仔细考虑之前就先喊出来了，真的是不明智的选择，也和夏何之前教我的不一样。"
    "但是我还是干了，这可难收场了。"
    "我成了全场的焦点。"
    "甚至比刚才的男生还要引人注目，因为我打断了他。"
    hide x with dissolve
    "男路人B" "「怎么了？」"
    "强忍着尴尬，他转向我，嘴角扯出一个显然不是真心的笑容，问道。"
    m "其实，夏何并不想和你交往。"
    "完了，话从嘴里说出来那一刻，我就后悔了。"
    "全场哗然。"
    "夏何眼里的慌张却消失了，她在笑。笑我的惊慌失措吗？"
    "男路人B" "「你怎么知道的？」"
    "虽然感觉到了明显的不耐烦甚至可以称之为恶意。但他还是客气地问了出来"
    "但我无话可说。我总不能说她是个死宅不喜欢你喜欢纸片人。"
    "我总不能说我从她眼里看出来她不喜欢你。"
    "我总不能说，我不希望你和她成为男女朋友。"
    show x summer normal medium with dissolve
    x "对不起，你很优秀，但是你来晚了。"
    "再一次，全场哗然。"
    show x summer happy medium with dissolve
    x "对不起，我有喜欢的人了。我有男朋友了，之前一直不承认是怕你们介意但是现在看来不说出来不行了。"
    "夏何的眼神好像重新有了光。"
    "友人AB" "「那你们是串通好了来骗我的吗？」"
    x "谁知道呢"
    return

label choice_16b:
    "我避开了夏何的视线钻到了人群里去。"
    "我逃离了酒吧。"
    "我把夏何拉黑了。"
    "我怕了，我没有帮助夏何的能力，我只是个普通阿宅，我不可能在那个时候救她"
    "我没有再在动漫社见过夏何，我只听打工地方的店长说，夏何特意把我和她的班调开了。听夏何的朋友说，夏何同意了那个男生的请求现在是很好的男女朋友。"
    "后来我辞去了工作，渐渐的和夏何离开了我的世界。仿佛她没有来过"
    return
